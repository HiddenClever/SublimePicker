buildscript {
    repositories {
        mavenCentral()
        jcenter()
    }

    dependencies {
        classpath 'com.android.tools.build:gradle:2.2.2'
        classpath 'com.github.dcendents:android-maven-gradle-plugin:1.4.1'
    }
}

apply plugin: 'com.android.library'
apply plugin: 'maven'
apply plugin: 'com.jfrog.bintray'
apply plugin: 'maven-publish'
def currentVersion = '2.1.2'

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.3'

    lintOptions {
        disable 'MissingTranslation'
        disable 'Instantiatable'
        disable 'ResourceType'
    }
    defaultConfig {
        minSdkVersion 14
        targetSdkVersion 25
        versionCode 4
        versionName "2.1.2"
    }
    buildTypes {
        release {
            minifyEnabled false
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
        }
    }

    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }
}

dependencies {
    compile fileTree(include: ['*.jar'], dir: 'libs')
    compile 'com.android.support:appcompat-v7:25.3.1'
    compile 'com.android.support:support-v4:25.3.1'
    compile 'com.android.support:gridlayout-v7:25.3.1'
    compile 'com.android.support:support-annotations:25.3.1'
}

publishing{
    publications{
        mavenJava(MavenPublication){
            groupId 'com.github.sbgmobile'
            artifactId 'sublimepickerlibrary-sbgmobile'
            version currentVersion
            artifact("$buildDir/outputs/aar/sublimepickerlibrary-release.aar")
            pom.withXml {
                def dependenciesNode = asNode().appendNode('dependencies')
                //Iterate over the compile dependencies (we don't want the test ones), adding a <dependency> node for each
                configurations.compile.allDependencies.each {
                    if(it.group != null && (it.name != null || "unspecified".equals(it.name)) && it.version != null)
                    {
                        def dependencyNode = dependenciesNode.appendNode('dependency')
                        dependencyNode.appendNode('groupId', it.group)
                        dependencyNode.appendNode('artifactId', it.name)
                        dependencyNode.appendNode('version', it.version)
                    }
                }
            }
        }
    }
}

bintray {
    user = System.getenv('BINTRAY_USER')
    key = System.getenv('BINTRAY_KEY')
    publications = ['mavenJava']
    publish = true
    pkg {
        repo = 'SBGMobile-SublimePicker'
        name = 'SublimePicker'
        userOrg = 'sbgmobile'
        licenses = ['Apache-2.0']
        vcsUrl = 'https://github.com/SBGMobile/SublimePicker.git'
        version {
            name = currentVersion
            gpg{
                sign = true
            }
        }
    }
}